# ---------------------------------------
# BNF grammar for LTL-FO+
# ---------------------------------------

<φ>            := # Boolean connectives
                  ( <φ> ) ∧ ( <φ> ) | ( <φ> ) ∨ ( <φ> ) | ( <φ> ) → ( <φ> ) | ¬ ( <φ> ) |
                  # Temporal operators
                  F ( <φ> ) | G ( <φ> ) | X ( <φ> ) | ( <φ> ) U ( <φ> ) |
                  # First-order quantifiers
                  ∃ <var> ∈ <π> : ( <φ> ) | ∀ <var> ∈ <π> : ( <φ> ) |
                  # Atoms
                  <atom>;

<atom>         := <var_or_const> = <var_or_const> | 
                  <var_or_const> gt <var_or_const> | 
                  <var_or_const> lt <var_or_const>;

<var_or_const> := <var> | <const>;

<const>        := ^"[\w\d\s]+";

<var>          := ^[\w\d]+;

<π>            := ^/[\w/]+;